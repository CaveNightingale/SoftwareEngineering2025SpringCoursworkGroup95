plugins {
    id 'application'
    id "me.champeau.jmh" version "0.7.3"
}

repositories {
    mavenCentral()
}

var os = System.getProperty('os.name').toLowerCase(Locale.ROOT)
var isWindows = os.contains('win')
var isLinux = os.contains('linux')
var osName = isWindows ? 'win' : isLinux ? 'linux' : 'mac'

dependencies {
    // Guava
    implementation 'org.commonmark:commonmark:0.21.0'

    implementation 'com.google.guava:guava:33.3.1-jre'

    // JavaFX
    implementation "org.openjfx:javafx-controls:21:${osName}"
    implementation "org.openjfx:javafx-base:21:${osName}"
    implementation "org.openjfx:javafx-graphics:21:${osName}"
    implementation "org.openjfx:javafx-fxml:21:${osName}"
    implementation "org.openjfx:javafx-media:21:${osName}"
    implementation "org.openjfx:javafx-web:21:${osName}"

    // ControlsFX
    implementation 'org.controlsfx:controlsfx:11.2.2'

    // Commons
    implementation 'org.apache.commons:commons-csv:1.13.0'
    implementation 'org.apache.commons:commons-lang3:3.17.0'

    // Jackson
    implementation 'com.fasterxml.jackson.core:jackson-core:2.18.3'

    // JUnit Jupiter
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.35'
    jmh 'org.openjdk.jmh:jmh-core:0.9'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:0.9'
    jmh 'org.openjdk.jmh:jmh-generator-bytecode:0.9'
    jmh sourceSets.test.output

    // Apache Commons Math
    implementation 'org.apache.commons:commons-math3:3.6.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'io.github.software.coursework.App'
    applicationDefaultJvmArgs = [
        "--module-path=${configurations.runtimeClasspath.asPath}",
        "--add-modules=javafx.controls,javafx.fxml,javafx.web,javafx.media,java.desktop"
    ]
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

tasks.register('bench', JavaExec) {
    group = 'verification'
    description = 'Run the benchmarks and assert reaching the minimum throughput requirements'
    dependsOn tasks.jmhClasses

    classpath = sourceSets.jmh.runtimeClasspath
    mainClass = 'io.github.software.coursework.BenchmarkMain'
}